plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
}

group = 'com.serveressentials'
version = '2.0.7.3'
description = 'ServerEssentials Plugin'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.lucko.me' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'net.luckperms:api:5.4'

    implementation 'org.json:json:20240303'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.14.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.2'
    implementation 'org.yaml:snakeyaml:2.2'

    implementation('com.github.oshi:oshi-core:6.4.0') {
        exclude group: 'net.java.dev.jna'
    }
}

task apiJar(type: Jar) {
    archiveBaseName = 'serveressentials-api'
    archiveClassifier = ''

    from(sourceSets.main.output) {
        include 'com/serveressentials/api/**'
        exclude 'com/serveressentials/api/APIImpl.class'
        exclude 'com/serveressentials/api/*Impl.class'
    }

    exclude '**/vault/**'
    exclude '**/commands/**'
    exclude '**/listeners/**'
    exclude '**/managers/**'
}

shadowJar {
    archiveClassifier = ''
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.DSA'

    exclude 'com/serveressentials/api/APIImpl.class'
    exclude 'com/serveressentials/api/*Impl.class'

}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
    }
}

configurations.all {
    resolutionStrategy {
        force 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        api(MavenPublication) {
            artifact(apiJar) {
                classifier = ''
            }
            artifact(sourcesJar) {
                classifier = 'sources'
            }

            pom {
                name = 'ServerEssentialsAPI'
                description = 'Public API for ServerEssentials plugin'
                url = 'https://github.com/YourUsername/ServerEssentials'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'godlycow'
                        name = '_GodlyCow'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "LocalRepo"
            url = uri("${buildDir}/repo")
        }
    }
}

tasks.build {
    dependsOn shadowJar, apiJar
}

tasks.register('copyPlugin', Copy) {
    dependsOn shadowJar
    from tasks.shadowJar.get().archiveFile
    into file("${projectDir}/plugins")
}