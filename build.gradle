plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.serveressentials'
version = '1.10'
description = 'ServerEssentials Plugin'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' } // Paper API
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' } // PlaceholderAPI
    maven { url = 'https://repo.lucko.me' } // LuckPerms
    maven { url = 'https://repo.codemc.org/repository/maven-public/' } // VaultAPI
}

dependencies {
    // Paper API and common APIs
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'net.luckperms:api:5.4'

    // JSON + System Info (optional utilities)
    implementation 'org.json:json:20240303'
    implementation 'com.github.oshi:oshi-core:6.4.0'

    // Adventure Text & Bukkit Integration
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.14.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.2'

}


configurations.all {
    resolutionStrategy {
        force 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.shadowJar {
    archiveBaseName.set("ServerEssentials")
    archiveVersion.set("")
    archiveClassifier.set("")
}

tasks.build {
    dependsOn shadowJar
}

tasks.register('copyPlugin', Copy) {
    dependsOn shadowJar
    from tasks.shadowJar.get().archiveFile
    into file("${projectDir}/plugins")
}
