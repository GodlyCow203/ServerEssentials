plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
}

group = 'com.serveressentials'
version = '2.0.7.9'
description = 'ServerEssentials Plugin'


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
}

def distDir = layout.projectDirectory.dir("dist/plugins")
def buildHistoryDir = file("${projectDir}/build-history")

def runDir = file("run/server/paper")
def runPluginsDir = file("${runDir}/plugins")
def paperJar = file("${runDir}/paper.jar")


tasks.register('downloadPaper') {
    outputs.file(paperJar)

    doLast {
        if (!paperJar.exists()) {
            runDir.mkdirs()

            def version = "1.21.1"
            def apiUrl = "https://api.papermc.io/v2/projects/paper/versions/${version}"

            def json = new groovy.json.JsonSlurper().parseText(
                    new URL(apiUrl).text
            )

            def build = json.builds.last()
            def jarUrl = "https://api.papermc.io/v2/projects/paper/versions/${version}/builds/${build}/downloads/paper-${version}-${build}.jar"

            println "Downloading Paper ${version} build ${build}"
            paperJar.bytes = new URL(jarUrl).bytes
        } else {
            println "Paper jar already exists, skipping download."
        }
    }
}



tasks.register('copyPlugin', Copy) {
    dependsOn tasks.named('shadowJar')
    from(distDir)
    include '*.jar'
    exclude 'serveressentials-api*.jar'
    exclude '*-sources.jar'
    into runPluginsDir
}




tasks.register('prepareRunServer') {
    dependsOn tasks.named('downloadPaper'), tasks.named('copyPlugin')

    doLast {
        runDir.mkdirs()
        runPluginsDir.mkdirs()

        def eula = file("${runDir}/eula.txt")
        if (!eula.exists()) {
            eula.text = "eula=true\n"
        }

        def props = file("${runDir}/server.properties")
        if (!props.exists()) {
            props.text = """
server-port=25565
online-mode=false
motd=ServerEssentials Dev Server
""".stripIndent()
        }
    }
}


tasks.register('runPaper', Exec) {
    dependsOn tasks.named('prepareRunServer')

    workingDir runDir
    commandLine(
            "java",
            "-Xms1G",
            "-Xmx2G",
            "-jar",
            "paper.jar",
            "--nogui"
    )
    standardInput = System.in
}


repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.lucko.me' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
}


dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'net.luckperms:api:5.4'

    implementation 'org.json:json:20240303'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.14.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.2'
    implementation 'org.yaml:snakeyaml:2.2'

    implementation('com.github.oshi:oshi-core:6.4.0') {
        exclude group: 'net.java.dev.jna'
    }
}


sourceSets {
    main {
        java {
            srcDirs = ['serveressentials-plugin/src/main/java']
        }
        resources {
            srcDirs = ['serveressentials-plugin/src/main/resources']
        }
    }
}


def isWindows = System.getProperty("os.name").toLowerCase().contains("windows")
def firebaseCmd = isWindows
        ? "${System.getenv('APPDATA')}\\npm\\firebase.cmd"
        : "firebase"


tasks.register('deployWeb', Exec) {
    description = 'Deploy the Web/ folder to Firebase Hosting'
    group = 'deployment'
    workingDir = file('Web')
    commandLine firebaseCmd, 'deploy', '--only', 'hosting'
}

tasks.register('serveWeb', Exec) {
    description = 'Start local Firebase emulator for the site'
    group = 'deployment'
    workingDir = file('Web')
    commandLine firebaseCmd, 'emulators:start', '--only', 'hosting'
}


tasks.register('apiJar', Jar) {
    archiveBaseName = 'serveressentials-api'
    archiveClassifier = ''
    destinationDirectory.set(distDir)

    from(sourceSets.main.output) {
        include 'com/serveressentials/api/**'
        exclude 'com/serveressentials/api/APIImpl.class'
        exclude 'com/serveressentials/api/*Impl.class'
    }

    exclude '**/vault/**'
    exclude '**/commands/**'
    exclude '**/listeners/**'
    exclude '**/managers/**'
}


tasks.withType(com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configureEach {
    archiveClassifier = ''
    destinationDirectory.set(distDir)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/*.DSA'

    exclude 'com/serveressentials/api/APIImpl.class'
    exclude 'com/serveressentials/api/*Impl.class'
}


tasks.withType(Jar).configureEach {
    destinationDirectory.set(distDir)
}


tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
    }
}


configurations.all {
    resolutionStrategy {
        force 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


publishing {
    publications {
        api(MavenPublication) {
            artifact(tasks.apiJar)
            artifact(sourcesJar) {
                classifier = 'sources'
            }

            pom {
                name = 'ServerEssentialsAPI'
                description = 'Public API for ServerEssentials plugin'
                url = 'https://github.com/godlycow203/ServerEssentials'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'godlycow'
                        name = '_GodlyCow'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "LocalRepo"
            url = uri("${buildDir}/repo")
        }
    }
}


tasks.build {
    dependsOn shadowJar, apiJar
}


gradle.buildFinished { result ->
    buildHistoryDir.mkdirs()

    def timestamp = new Date().format("yyyy-MM-dd_HH-mm-ss")
    def status = result.failure == null ? "SUCCESS" : "FAILED"
    def file = new File(buildHistoryDir, "${timestamp}_${status}.txt")

    file.text = """
Build Status : ${status}
Project      : ${project.name}
Version      : ${project.version}
Time         : ${new Date()}
Java         : ${System.getProperty("java.version")}

${status == "FAILED" ? "Error:\\n${result.failure}" : ""}
""".stripIndent()
}
