plugins {
    id "java"
    id "com.gradleup.shadow" version "8.3.5"
}

version = "3.0.0.1"

dependencies {
    implementation project(":api")

    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.5"
    compileOnly "net.milkbowl.vault:VaultAPI:1.7"
    compileOnly "net.luckperms:api:5.4"

    implementation "org.json:json:20240303"
    implementation "com.zaxxer:HikariCP:5.0.1"
    implementation "net.kyori:adventure-text-minimessage:4.14.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.14.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.2"
    implementation "org.yaml:snakeyaml:2.2"
    implementation("com.github.oshi:oshi-core:6.4.0") {
        exclude group: "net.java.dev.jna"
    }
}

processResources {
    filesMatching("plugin.yml") {
        expand(
                name: rootProject.name,
                version: project.version,
                main: "net.godlycow.org.EssC"
        )
    }
}

shadowJar {
    archiveFileName = "EssentialsC-${project.version}.jar"
    destinationDirectory = rootProject.layout.projectDirectory.dir("dist/plugins")
    exclude "META-INF/**"
    configurations = [project.configurations.runtimeClasspath]
}

tasks.jar.enabled = false
tasks.build.dependsOn tasks.shadowJar

tasks.register("setUp") {
    group = "build"
    description = "Cleans plugin folders, builds the shadow jar, and copies it to server and dist."

    dependsOn "shadowJar"

    doLast {
        def pluginName = "EssentialsC"
        def jarPattern = ~/ServerEssentials-.*\.jar/
        def paperPluginsDir = file("paper-server/run/paper/plugins")
        def distPluginsDir = file("dist/plugins")
        def pluginFolderInPaper = new File(paperPluginsDir, pluginName)

        paperPluginsDir.mkdirs()
        distPluginsDir.mkdirs()

        paperPluginsDir.listFiles()?.each { file ->
            if (file.name ==~ jarPattern) {
                println "Deleting old plugin jar: ${file}"
                file.delete()
            }
        }

        if (pluginFolderInPaper.exists()) {
            println "Deleting plugin folder: ${pluginFolderInPaper}"
            pluginFolderInPaper.deleteDir()
        }

        if (distPluginsDir.exists()) {
            println "Deleting dist/plugins contents"
            distPluginsDir.listFiles()?.each { it.delete() }
        }

        def builtJar = tasks.shadowJar.get().archiveFile.get().asFile
        println "Copying jar to paper plugins..."
        copy { from builtJar; into paperPluginsDir }
        println "Copying jar to dist/plugins..."
        copy { from builtJar; into distPluginsDir }

        println "SetUp task completed successfully!"
    }
}
